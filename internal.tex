This section applies the idea of dualization in \autoref{chap:dualize} to the
ITree context, showing how to address internal nondeterminism by symbolic
evaluation based on ITree specifications.  It covers the derivation path from
``symbolic model'' to ``nondeterministic tester'' in \autoref{fig:framework},
using \http entity tags introduced in \autoref{sec:internal-nondeterminism} as
an example.

As discussed in \autoref{sec:encode-spec}, dualization requires refining the
representation of the server's computation {\it e.g.} encoding its branches over
symbolic conditions.  This is done by designing ITrees' event types in
\autoref{sec:symbolic-model} and specifying the server's behavior with a
symbolic model.

The server specification is derived into a tester client by {\em interpreting}
interaction trees.  To interpret is to define semantic rules that transform one
ITree program into another, and corresponds to the arrows in
\autoref{fig:framework}.  \autoref{sec:interp} explains the interpretation of
ITrees.

The interpretation from symbolic model to the nondeterministic tester model is
implemented in two phases, illustrated as ``dualization'' and ``unification''
arrows in \autoref{fig:framework}: \autoref{sec:dualize-interaction} dualizes
the server's behavior into the tester client's, resulting in a ``symbolic
observer'' that encodes symbolic evaluation as primitive events.
\autoref{sec:symbolic-eval} then instantiates the primitive events into
pure computations that unify concrete observations against their symbolic
representations.

\subsection{Symbolic server model}
\label{sec:symbolic-model}
The server specification is an ITree program that exhibits all valid behavior of
the protocol.  I combine the $\Prog$ language in \autoref{sec:prog-lang} with the
simplistic ITree example in \autoref{sec:qac-itree}.

\paragraph{Network packet type}
Instead of receiving requests and sending responses, the server receives and
sends {\em packets} that carry routing information.  This allows us to specify
the server's interaction against concurrent clients in
\autoref{sec:external-nondet}.  A packet consists of headers that indicate its
source and destination, and a payload of either a request or a response:
\begin{coq}
  Notation connection := N. (* N for natural number *)

  Record packet Q A := {
    Source      : endpoint;
    Destination : endpoint;
    Payload     : Q + A
  }.
\end{coq}
This type definition says: the \ilc{packet} type is parameterized over the \ilc
Q and \ilc A types that represent its request and response.  Its \ilc{Source}
and \ilc{Destination} fields each records an \ilc{endpoint} represented as a
natural number.  Its \ilc{Payload} type is the sum of request and response.

Here's an example trace of network packets:
\begin{coq}
  Context get: string -> request.
  Context ok : string -> response.

  Definition server_end: endpoint := 0.

  Example trace: list (packet request response) :=
    [ { Source      := 1;
        Destination := server_end;
        Payload     := inl (get "/index.html")
      }
    ; { Source      := server_end;
        Destination := 1;
        Payload     := inr (ok "<p>Hello!</p>")
      }
    ].
\end{coq}
This trace encodes a transaction between client 1 and the server (represented as
endpoint 0).  The client sends a GET request to fetch the resource in path
\ilc{"/index.html"}, and the server responds with 200 OK and content
\ilc{"<p>Hello!</p>"}.  The \ilc{inl} and \ilc{inr} are constructors for sum
types:
\begin{coq}
  Context inl: forall {X Y}, X -> X + Y.
  Context inr: forall {X Y}, Y -> X + Y.
\end{coq}

\paragraph{Symbolic representation}
To specify systems' nondeterministic behavior, the $\Prog$ language in
\autoref{sec:prog-lang} encodes data as symbolic expressions $\Sexp$, so that
the responses and branch conditions may depend on internal choices.  I do the
same for ITree specifications, by symbolizing the choice events and branch
conditions.  Take my HTTP specification~\cite{issta21} as an example.  Its
choice event has symbolic expression as result type:

\begin{coq}
  Variant comparison := Strong | Weak.

  Variant exp: Type -> Type :=
    Const    : string -> exp string
  | Var      : var    -> exp string
  | Compare  : string -> exp string -> comparison -> exp bool.

  Variant choiceE: Type -> Type :=
    Choice: choiceE (exp string).
\end{coq}

Here I instantiate the \ilc{choiceE} in \autoref{sec:itree-lang} with symbolic
return type \ilc{(exp string)}, pronounced ``expression of type string''.  In
this example, I use strings to represent entity tags (ETags) that HTTP servers
may generate, which was discussed in \autoref{sec:internal-nondeterminism}.  The
type interface can be adjusted to other protocols under test.

Symbolic expressions may be constructed as constant values, as variables, or
with operators.  Here is an example of expressions of type string:
\begin{coq}
  Context x y : var.

  Example expressions: list (exp string) :=
    [ Const   "foo"
    ; Var      x
    ; Compare "bar" (Var y) Weak
    ].
\end{coq}

The \ilc{Compare} constructor takes an expression of type string and compares it
against a constant string.  \ilc{(Compare t tx cmp)} represents the ETag
comparison between \ilc{t} and \ilc{tx}, using ``strong comparison'' or ``weak
comparison'' mechanism\footnote{\http servers may choose to generate ETags as
``strong validators'' (with uniqueness guarantee) or ``weak validators'' (for
potentially better performance).  Weak validators have prefix \iletag{W/} while
strong validators do not.  When handling compare-and-swap operations such as PUT
requests conditioned over \inlinec{If-Match} in
\autoref{sec:internal-nondeterminism}, the server should evaluate its
precondition with ``strong comparison'' that doesn't allow weak validators ({\it
e.g.} \iletag{W/"foo"}) to match any ETag including itself.  For GET requests
conditioned over \inlinec{If-None-Match}, the server may evaluate with ``weak
comparison'' where a weak validator like \iletag{W/"bar"} matches itself and
also matches strong validator \iletag{"bar"}, but doesn't match \iletag{W/"foo"}
or \iletag{"foo"}.} specified by \ilc{cmp}.  The constant ETag is provided by
the request, and the symbolic one comes from the server state.

\begin{figure}
\begin{lstlisting}[numbers=left]
  Notation sigma := (path -> resource).

  Context OK PreconditionFailed : symbolic_response.
  Context process: request -> sigma -> itree smE (symbolic_response * sigma).

  CoFixpoint server_http (state: sigma) :=
    pq <- trigger Recv;;
    let respond_with a :=
      trigger (Send { Source      := server_conn
                    ; Destination := pq.(Source)
                    ; Payload     := inr a } ) in
    let q : request    := request_of pq        in
    let v : string     := q.(Content)          in
    let k : path       := q.(TargetPath)       in
    let t : string     := if_match q           in
    let tx: exp string := (state k).(ETag)     in
    IFX (Compare t tx Strong)%\label{line:etag-ifx}%
    THEN
      if q.(Method) is Put%\label{line:etag-pure-if}%
      then
        tx' <- or (trigger Choice)%\label{line:etag-choice}%
                  (Pure (Const EmptyString));;
        let state' := state [k |-> {Content := v; ETag := tx'}] in
        respond_with OK;;
        server_http state'
      else                 (* handling other kinds of requests *)
        (a, state') <- process q state;;
        respond_with a;;
        server_http state'
    ELSE
      respond_with PreconditionFailed;;
      server_http s.%\label{line:etag-end}%
\end{lstlisting}
\caption{Server model for HTTP conditional requests}
\label{fig:if-match-server}
\end{figure}

\autoref{fig:if-match-server} shows an ITree model for If-Match requests 
(\autoref{sec:internal-nondeterminism}).  The server state \ilc{sigma} \lys{I
used \ilc{sigma} to represent the server state in QAC, so keeping this
notation.}  maps each path to its corresponding ``resource''---file content and
metadata like ETag.  The server first evaluates the request's \inlinec{If-Match}
condition by ``strong comparison'' as required by HTTP.  If the request's ETag
matches its target's, then the server updates the target's contents with the
request payload.  The target's new ETag \ilc{tx'} is permitted to be any value,
so the model represents it as \ilc{Choice} event.

Notice that the server model exhibits three kinds of branches:
\begin{enumerate}
\item The \ilc{if} branch in \autoref{line:etag-pure-if} is provided by ITree's
embedding language Coq, and takes a boolean value as condition;
\item The \ilc{IFX} branch in \autoref{line:etag-ifx} constructs an ITree that
nondeterministically branches over a condition written as a symbolic expression
of type bool:
\begin{coq}
  Variant branchE: Type -> Type := (* symbolic-conditional branch *)
    Decide: exp bool -> branchE bool.

  Notation "IFX condition THEN x ELSE y" :=
    (b <- trigger (Decide condition);;
     if b then x else y).
\end{coq}
\item The \ilc{or} operator in \autoref{line:etag-choice} takes two ITrees
\ilc x and \ilc y as possible branches and constructs a nondeterministic program
that may behave as either \ilc x or \ilc y:
\begin{coq}
  Variant nondetE: Type -> Type := (* nondeterministic branch *)
    Or: nondetE bool.

  Definition or {E R} `{nondetE -< E} (x y: itree E R) : itree E R :=
    b <- trigger Or;;
    if b then x else y.
\end{coq}
Here \ilc{nondetE} is a special case of \ilc{choiceE} with boolean space of
choices, but they'll be handled differently during test derivation.  The type
signature \ilc{\{E R\} `\{nondetE -< E\}} says the \ilc{(or)} combinator can
apply to ITrees whose event \ilc E is a super-event of \ilc{nondetE}, and with
arbitrary return type \ilc R.  For example, arguments \ilc x and \ilc y can be
of type \ilc{(itree (qaE +' nondetE +' choiceE +' branchE) void)}.
\end{enumerate}

These three kinds of branch conditions play different roles in the
specification, and will be handled differently during testing:
\begin{enumerate}
\item The ``pure'' \ilc{if} condition is used for deterministic branches like
  \ilc{(q.(Method) is Put)} in the example.  Here \ilc q is a ``concrete
  request''---a request that doesn't involve symbolic variables, as opposed to
  ``symbolic'' ones---generated by the client and sent to the server, so its
  method is known by the tester and needn't be symbolically evaluated.
\item The ``symbolic'' \ilc{IFX} condition here plays a similar rule as the
  $\mathsf{if}$ branches in the $\Prog$ language: Which branch to take depends
  on the server's internal choices, so the tester needs to consider both cases.
\item The \ilc{or} branch defines multiple control flows the server may take.
  In the HTTP example, the server may generate an ETag for the resource's new
  content, but is not obliged to do so.  It may choose to generate no ETags
  instead, using \ilc{(Pure (Const EmptyString))} as an alternative
  to \ilc{(trigger Choice)}.
\end{enumerate}

In addition to \ilc{IFX} branch conditions, the symbolic expressions may also
appear in the server's responses.  For example, after generating an ETag in
\autoref{line:etag-choice} of \autoref{fig:if-match-server}, the server may
receive a GET request and send the ETag to the client:
\begin{coq}
  Example ok_with_etag: symbolic_response :=
    { ResponseLine := { Version := { Major := 1; Minor := 1 }
                      ; Code    := 200
                      ; Phrase  := OK
                      }
    ; ResponseFields :=
      [ { Name := "Content-Length"; Value := Const "13" }
      ; { Name := "ETag"          ; Value := Var    x   }
      ]
    ; ResponseBody := "<p>Hello!</p>"
    }.
\end{coq}
Suppose the server generated the ETag as expression \ilc{(Var x)}, then we can
use the expression to construct the symbolic response in the specification,
rather than determining its concrete value.  The mechanism of producing
expressions for ETags is explained in \autoref{sec:dualize-interaction}.

Now we can define the specification's event type \ilc{smE}.  The symbolic server
model receives concrete requests and sends symbolic responses, so its event is
defined as:
\begin{coq}
  Definition symbolic_packet := packet request symbolic_response.%\label{def:symbolic-packet}%

  Definition qaE := ioE symbolic_packet symbolic_packet.%\label{def:symbolic-qae}%

  Notation smE := (qaE +' nondetE +' choiceE +' branchE).
\end{coq}
The ``Symbolic Model'' in \autoref{fig:framework} is an ITree constructed by
applying the \ilc{server_http} function to an initial state:
\begin{coq}
  Definition sm_http: itree smE void :=
    server_http init_state.
\end{coq}

The rest of this section will explain the interpretations from this symbolic
model.

\subsection{Dualizing symbolic model}
\label{sec:dualize-interaction}
This subsection takes the symbolic model composed in
\autoref{sec:symbolic-model} and dualizes its interactions, which corresponds
to the ``Dualization'' arrow in \autoref{fig:framework}.  It applies the idea of
derivation rules (\ref{rule:write})--(\ref{rule:return}) for $\Prog$
(\autoref{sec:dualize-prog}) to models written as ITrees.

This interpretation phase produces a symbolic observer that models the tester's
observation and validation behavior.  The observer sends a request when the
server wants to receive one, and receives a response when the server wants to
send one.  It also creates constraints over the server's internal choices based
on its observations.

\autoref{fig:symbolic-observer} shows the dualization algorithm.  It interprets
the symbolic model's events with the \ilc{observe} handler, whose types are
explained as follows:

\begin{figure}
\begin{lstlisting}[numbers=left]
Notation oE := (observeE +' nondetE +' choiceE +' constraintE).

Definition observe {R} (e: smE R) : itree oE R :=
  match e with
  | Recv      => trigger FromObserver%\label{line:observe-absorb}%
  | Send px   => p <- trigger ToObserver;;%\label{line:observe-emit}%
                 trigger (Guard px p)
  | Decide bx => or (trigger (Unify bx true);;  ret true)%\label{line:observe-branch}%
                    (trigger (Unify bx false);; ret false)
  | Or        => trigger Or%\label{line:observe-or}%
  | Choice    => trigger Choice%\label{line:observe-choice}%
  end.

Definition observer_http: itree oE void :=
  interp observe sm_http.
\end{lstlisting}
\caption{Dualizing symbolic model into symbolic observer.}
\label{fig:symbolic-observer}
\end{figure}

The tester observes a trace of concrete packets, so observer's interactions
return concrete requests and responses, as opposed to the symbolic model whose
responses are symbolic.
\begin{coq}
  Definition concrete_packet := packet request concrete_response.

  Variant observeE : Type -> Type :=
    FromObserver   : observeE concrete_packet
  | ToObserver     : observeE concrete_packet.
\end{coq}

Notice that the observer's send and receive events both return the packet sent
or received, unlike the server model whose \ilc{Send} event takes the sent
packet as argument.  This is because the tester needs to generate the request
packet to send, and the event's result value represents that generated and sent
packet.

As discussed in \autoref{sec:dualize-prog}, when the server sends a symbolic
response or branches over a symbolic condition, the tester needs to create
symbolic constraints accordingly.  The observer introduces ``constraint events''
to represent the creation of constraints primitively.
\begin{coq}
  Variant constraintE : Type -> Type :=
    Guard : packet -> concrete_packet -> constraintE unit
  | Unify : exp bool -> bool -> constraintE unit.
\end{coq}

Here \ilc{(Guard px p)} creates a constraint that the symbolic packet \ilc{px}
emitted by the specification matches the concrete packet \ilc p observed during
runtime.  \ilc{(Unify bx b)} creates a constraint that unifies the symbolic
branch condition \ilc{bx} with boolean value \ilc b.  These constraints will be
solved in \autoref{sec:symbolic-eval}.

The dualization algorithm in \autoref{fig:symbolic-observer} does the follows:
\begin{enumerate}
  \item When the symbolic model absorbs a packet in
    \autoref{line:observe-absorb}, the observer generates a request packet;
  \item When the symbolic model emits a symbolic packet \ilc{px} in
    \autoref{line:observe-emit}, the observer receives a concrete packet \ilc p,
    and adds a constraint that restricts the symbolic and concrete packets to
    match each other.
  \item When the symbolic model branches on a symbolic condition \ilc{bx} in
    \autoref{line:observe-branch}, the tester accepts the observation if it can
    be explained by any branch.  This is done by constructing the observer as a
    nondeterministic program that has both branches, using the \ilc{or}
    combinator.  For each branch, the observer adds a constraint that the
    symbolic condition matches the chosen branch.
  \item Nondeterministic branches in \autoref{line:observe-or} are preserved in
    this interpretation phase, and will be resolved in \autoref{sec:backtrack}.
  \item Internal choices in \autoref{line:observe-choice} are addressed by the
    next phase in \autoref{sec:symbolic-eval}, along with the constraints
    created in this phase.
\end{enumerate}

The result of dualization is a symbolic observer that models tester behaviors
like sending requests and receiving responses.  The symbolic observer is a
nondeterministic program with primitives events like making choices and adding
constraints over the choices.

For example, dualizing \autoref{line:etag-ifx}--\ref{line:etag-end} in
\autoref{fig:if-match-server} results in an observer program as shown in
\autoref{fig:observer-example}.  The next subsection interprets the observer's
primitive \ilc{Guard} and \ilc{Unify} events into pure computations of symbolic
evaluation.

\begin{figure}
\begin{coq}
  Example observer_body: itree oE void :=
    let guard_response a :=
      p <- trigger ToObserver;;
      trigger (Guard { Source      := server_conn
                     ; Destination := pq.(Source)
                     ; Payload     := inr a } ) in
    let bx: exp bool := Compare t tx Strong in
    or (
        trigger (Unify (Compare bx true));;
        if q.(Method) is Put
        then
          tx' <- or (trigger Choice)
                    (Pure (Const EmptyString));;
          let state' := state [k |-> {Content := v; ETag := tx'}] in
          guard_response OK;;
          interp observe (server_http state')
        else
          (a, state') <- interp observe (process q state);;
          guard_response a;;
          interp observe (server_http state')
       )
       (
        trigger (Unify (Compare bx false));;
        guard_response PreconditionFailed;;
        interp observe (server_http s)
       ).
\end{coq}
\caption{Symbolic observer example.}
\label{fig:observer-example}
\end{figure}


\subsection{Symbolic evaluation}
\label{sec:symbolic-eval}
This subsection takes the symbolic observer produced in
\autoref{sec:dualize-interaction} and solves the constraints it has created.
The constraints unify symbolic packets and branch conditions against the
concrete observations.  The tester should accept the SUT if the constraints are
satisfiable.

\begin{figure}
\begin{lstlisting}[numbers=left]
Notation ntE := (observeE +' nondetE +' exceptE).

Definition V: Type := list var * list (constraintE unit).
  
Definition unify {R} (e: oE R) (v: V) : itree ntE (V * R) :=
  let (xs, cs) := v in
  match e with
  | Choice => let x: var := fresh v in%\label{line:unify-choice}%
              ret (x::xs, cs, Var x)
  | (constraint: unifyE) => let cs' := constraint::cs in%\label{line:unify-constraint}%
                            if solvable cs'
                            then ret (xs, cs', tt)
                            else Trigger (Throw ("Conflict: " ++ print cs'))
  | Or             => b <- trigger Or;; ret (v, b)%\label{line:unify-or}%
  | (oe: observeE) => r <- trigger oe;; ret (v, r)%\label{line:unify-observe}%
  end.

Definition nondet_tester_http: itree ntE void :=
  (_, vd) <- interp_state unify observer_http initV;;
  match vd in void with end.
\end{lstlisting}
\caption{Resolving symbolic constraints.}
\label{fig:nondet-tester}
\end{figure}

As shown in \autoref{fig:nondet-tester}, the unification algorithm evaluates the
primitive symbolic events into a stateful checker program, which reflects the
$\Prog$-based validator in \autoref{sec:dualize-prog}.  The interpreter
maintains a validation state \ilc V which stores the symbolic variables and the
constraints over them.  The derivation rules are as follows:
\begin{enumerate}
  \item When the server makes an internal choice in \autoref{line:unify-choice},
    the tester creates a fresh variable and adds it to the validation state.
  \item When the observer creates a constraint in
    \autoref{line:unify-constraint}, the tester adds the constraint to the
    validation state, and solves the new set of constraints.  If the constraints
    become unsatisfiable, then the tester \ilc{Throw}s an exception that
    indicates the current execution branch cannot accept the observations:
\begin{coq}
  Variable exceptE: Type -> Type :=
    Throw: forall {X}, string -> exceptE X.
\end{coq}
  \item The observer is a nondeterministic program with multiple execution
    paths, constructed by \ilc{Or} events in \autoref{line:unify-or}.  The
    tester accepts the observation if any of the branches does not throw an
    exception.  These branches will be handled in the next section, along with
    the observer's send/receive interactions in \autoref{line:unify-observe}.
\end{enumerate}

Notice that the \ilc{unify} function interprets a symbolic observer's event
\ilc{(oE R)} into a function of type \ilc{(V -> itree tE (V * R))},
called ``state monad transformer''.  It takes a pre-validation state \ilc{(v:
V)} and computes an ITree that yields the observer event's corresponding result
\ilc{(r: R)} along with a post-validation state \ilc{(v': V)}.  This stateful
interpretation process is implemented by a variant of \ilc{interp} called
\ilc{interp_state}:
\begin{coq}
  CoFixpoint interp_state {E F V R}
                          (handler: forall {X}, E X -> V -> itree F (V * X))
                          (m: itree E R) (v: V)
             : itree F (V * R) :=
    match m with
    | Pure   r   => ret (v, r)
    | Impure e k => '(v', r) <- handler e v;;
                    interp_state handler (k r) v'
    end.
\end{coq}

So far I have interpreted the symbolic model into a tester model that observes
incoming and outgoing packets, nondeterministically branches, and in some cases
throws exceptions.  In this section, I used the server model verbatim as the
symbolic model, and the derived tester can handle internal nondeterminism by
symbolic evaluation.

The server model's receive and send events are linear.  It doesn't receive new
requests before responding to its previous request.  As a result, the tester
dualized from the server model doesn't send new requests before it receives the
response to the previous request, so it doesn't reveal the server's interaction
with multiple clients simultaneously.  In the next section, I'll explain how to
create a multi-client tester by extending the symbolic model, addressing
external nondeterminism.
