This section takes the nondeterministic tester model derived in
\autoref{sec:symbolic-eval} and transforms it into an interactive program.
\autoref{sec:backtracking} handles the nondeterministic branches via
backtracking and produces a deterministic tester model.  \autoref{sec:itree-io}
then interprets the deterministic tester into an IO program that interacts with
the SUT.

\subsection{Backtrack execution}
\label{sec:backtracking}
This subsection explains how to run the nondeterministic tester on a
deterministic machine.  It reflects the derivation rules (\ref{rule:unsat}) and
(\ref{rule:reject}) for $\Prog$ in \autoref{sec:dualize-prog}, and constructs
the ``Backtracking'' arrow in \autoref{fig:framework}.

The deterministic tester implements a client that sends and receives concrete
packets:
\begin{coq}
  Variant clientE: Type -> Type :=
    ClientSend: concrete_packet -> clientE unit
  | ClientRecv: clientE (option concrete_packet).
\end{coq}

Note that the \ilc{ClientRecv} event might return \ilc{(Some pkt)}, indicating
that the SUT has sent a packet \ilc{pkt} to the tester; or it might
return \ilc{None}, when the SUT is silent, or its sent packet hasn't arrived at
the tester side.  This allows the tester to perform non-blocking interactions,
instead of waiting for the SUT, which might cause starvation.

\begin{figure}
\begin{lstlisting}[numbers=left]
Notation tE := (clientE +' genE +' exceptE).

CoFixpoint backtrack (current:      itree ntE void)
                     (others: list (itree ntE void))
           : itree tE void :=
  match current with
  | Impure e k =>
    match e with
    | Or           => b <- trigger GenBool;;%\label{line:backtrack-or}%
                      backtrack (k b) (k (negb b)::others)
    | Throw msg    => match others with%\label{line:backtrack-throw}%
                      | other::ot' => backtrack other ot'
                      | []         => trigger (Throw msg)
                      end
    | FromObserver => q <- trigger GenPacket;;%\label{line:backtrack-send}%
                      trigger (ClientSend q);;
                      let others' := expect FromObserver q others in
                      backtrack (k q) others'
    | ToObserver   =>
      oa <- trigger ClientRecv;;%\label{line:backtrack-recv}%
      match oa with
      | Some oa => let others' := expect ToObserver a others in
                   backtrack (k a) others'
      | None    =>%\label{line:backtrack-silent}%
        match others with
        | other::ot' => backtrack other (ot'++[current]) (* postpone *)%\label{line:backtrack-postpone}%
        | []         => backtrack m     []               (* retry    *)
        end
      end
    end
  | Pure vd => match vd in void with end
  end.

Definition tester_http: itree tE void :=
  backtrack nondet_tester_http [].
\end{lstlisting}
\vspace*{1em}
\caption{Backtrack execution of nondeterministic tester.}
\label{fig:backtrack}
\end{figure}

\autoref{fig:backtrack} shows the backtracking algorithm.  It interacts with the
SUT and checks whether the observations can be explained by the nondeterministic
tester model.  That is, checking whether the tester has an execution path that
matches its interactions.  This is done by maintaining a list of all possible
branches in the tester, and checking if any of them accepts the observation.

The tester exhibits two kinds of randomness: (1) When sending a request packet
to the SUT, it generates the packet randomly with \ilc{GenPacket}; (2) When the
nondeterministic tester model branches, the deterministic tester randomly picks
one branch to evaluate, using \ilc{GenBool}:
\begin{coq}
  Variant genE: Type -> Type :=
    GenPacket : genE concrete_packet
  | GenBool   : genE bool.
\end{coq}

\begin{figure}
\begin{lstlisting}[numbers=left]
CoFixpoint match_observe {R} (e: observeE R) (r: R)
                             (m: itree ntE (V * void))
           : itree ntE (V * void) :=
  match m with
  | Impure (oe: observeE concrete_packet) k =>
    match oe, e with
    | FromObserver, FromObserver%\label{line:match-from}%
    | ToObserver  , ToObserver => k r%\label{line:match-to}%
    | _, _ => trigger (Throw ("Expect " ++ print oe%\label{line:match-throw}%
                           ++ " but observed " ++ print e))
    end
  | Impure e0 k =>
    r0 <- trigger e0;;
    match_observe e r (k r0)
  | Pure (_, vd) => match vd in void with end
  end.

Definition expect {R} (e: observeE R) (r: R)
  : list (itree ntE (V * void)) -> list (itree ntE (V * void))
  := map (match_observe e r).
\end{lstlisting}
\vspace*{1em}
\caption{Matching tester model against existing observation.}
\label{fig:match-observe}
\end{figure}

The execution rule is defined as follows:
\begin{enumerate}
\item When the tester nondeterministically branches, in
\autoref{line:backtrack-or}, randomly pick a branch \ilc{(k b)} to evaluate and
push the other branch \ilc{(k (negb b))} to the list of other possible cases.

\item When the \ilc{current} tester throws an exception, in
\autoref{line:backtrack-throw}, it indicates that the current execution path
rejects the observations.  The tester should try to explain its observations
with other branches of the tester model.  If the \ilc{others} list is empty, it
indicates that the observation is beyond the specification's producible
behavior, so the tester should reject the SUT.

\item When the tester wants to observe a packet {\em from} itself, it generates
a packet and sends it to the SUT in \autoref{line:backtrack-send}.

Note that if the current branch is rejected and the tester backtracks to other
branches, the sent packet cannot be recalled from the environment.  Therefore,
all other branches should recognize this sent packet and check whether future
interactions are valid follow-ups of it.  This is done by matching the branches
against the send event, using the \ilc{expect} function.

  As shown in \autoref{fig:match-observe}, \ilc{(expect e r l)} matches every
  tester in list \ilc l against the observation \ilc e that has return
  value \ilc r.  For each element $\texttt m\in\texttt l$, if \ilc m's first
  observer event \ilc{oe} matches the observation \ilc e
  (\autoref{line:match-from} and \autoref{line:match-to}),
  then \ilc{match_observe} instantiates the tester's continuation function \ilc
  k with the observed result \ilc r.  Otherwise, the tester throws an exception
  in \autoref{line:match-throw}, indicating that this branch cannot explain the
  observation because they performed different events.
  \label{rule:backtrack-send}

\item When the current tester wants to observe a packet {\em to} itself, it
  triggers the \ilc{ClientRecv} event in \autoref{line:backtrack-recv}.  If a
  packet has indeed arrived, then it instantiates the current branch as well as
  other possible branches, as discussed in Rule~(\ref{rule:backtrack-send}).

  If the tester hasn't received a packet from the SUT
  (\autoref{line:backtrack-silent}), it doesn't reject the SUT, because the
  expected packet might be delayed in the environment.  If there are \ilc{other}
  branches to evaluate (\autoref{line:backtrack-postpone}), then the tester
  postpones the \ilc{current} branch by appending it to the back of the queue.
  Otherwise, if the current branch is the only one that hasn't rejected, then
  the tester retries the receive interaction.

  Note that if the SUT keeps silent, then the tester will starve but won't
  reject, because (i) such silence is indistinguishable from the SUT sending a
  packet that is delayed by the environment, and (ii) the SUT hasn't {\em
  exhibited} any violations against the specification.  The starvation issue is
  addressed in \autoref{sec:itree-io}.
\end{enumerate}

The backtracking algorithm also explains the network composition design
in \autoref{fig:net-compose-code}, where the server model is scheduled at a
higher priority than the network model.  Suppose the SUT has produced some
invalid output.  Then the tester should reject its observation by throwing an
exception in every branch.  However, the network model is always ready to absorb
a packet, because the \ilc{pick_one} function on Page~\pageref{def:pick-one}
always includes a branch that returns \ilc{None}.  If the composition algorithm
prioritizes the network model over the server model, then when one branch
rejects, the derived tester backtracks to other branches, which includes
generating and sending another packet to the SUT (dualized from the network
model's absorption event).  Evaluating the network model lazily prevents the
composed symbolic model from having infinitely many absorbing branches.  This
allows the derived tester to converge to rejection upon violation, instead of
continuously sending request packets.

Now we have derived the specification into a deterministic tester model in
ITree.  The tester's events reflect actual computations of a client program.  In
the next subsection, I'll translate the ITree model into a binary executable
that runs on silicon and metal.

\subsection{From ITree model to IO program}
\label{sec:itree-io}
The deterministic tester model derived in \autoref{fig:backtrack} is an ITree
program that never returns (its result type \ilc{void} has no elements).  It
represents a client program that keeps interacting with the SUT until it reveals
a violation and throws an exception.

In practice, if the tester hasn't found any violation after performing a certain
number of interactions, then it accepts the SUT.  This is done by executing the
ITree until reaching a certain depth.

\begin{figure}
\begin{lstlisting}[numbers=left]
Fixpoint execute (fuel: nat) (m: itree tE void) : IO bool :=
  match fuel with
  | O       => ret true            (* accept if out of fuel *)%\label{line:execute-accept}%
  | S fuel' =>
    match m with
    | Impure e k =>
      match e with
      | Throw _      => ret false  (* reject upon exception *)%\label{line:execute-reject}%
      | ClientSend q => client_send q;;
                        execute fuel' (k tt)
      | ClientRecv   => oa <- client_recv;;
                        execute fuel' (k oa)
      | GenPacket    => pkt <- gen_packet;;%\label{line:execute-gen}%
                        execute fuel' (k pkt)
      | GenBool      => b <- ORandom.bool;;
                        execute fuel' (k b)
      end
    | Pure vd => match vd in void with end
    end
  end.

Definition test_http: IO bool :=
  execute bigNumber tester_http.
\end{lstlisting}
\vspace*{1em}
\caption{Interpreting ITree tester to IO monad.}
\label{fig:execute}
\end{figure}

As shown in \autoref{fig:execute}, the \ilc{execute} function takes an
argument \ilc{fuel} that indicates the remaining depth to explore in the ITree.
If the execution ran out of fuel (\autoref{line:execute-accept}), then the test
accepts; If the tester model throws an exception
(\autoref{line:execute-reject}), then the test rejects.  Otherwise, it
translates the ITree's primitive events into IO computations in
Coq~\cite{SimpleIO}, which are eventually extracted into OCaml programs that can
be compiled into executables that can communicate with the SUT over the
operating system's network stack.

This concludes my validation methodology.  In this chapter, I have shown how to
test real-world systems that exhibit internal and external nondeterminism.  I
applied the dualization theory in \autoref{chap:theory} to address internal
nondeterminism and handled external nondeterminism by specifying the
environment's space of uncertainty.  The specification is derived into an
executable tester program by multiple phases of interpretations.  The
derivation framework is built on the ITree specification language, but the
method is applicable to other languages that allow destructing and analyzing the
model programs.

So far, I have answered ``how to tell compliant implementations from violating
ones''.  The next chapter will answer ``how to generate and shrink test inputs
that reveal violations effectively'' and unveil the techniques behind
\ilc{gen_packet} in \autoref{line:execute-gen} of \autoref{fig:execute}.
